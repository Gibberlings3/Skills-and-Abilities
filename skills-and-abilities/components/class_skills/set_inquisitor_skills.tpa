///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Sets Inquisitor Skills                                                //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Add Required Files to Override                                        //
///////////////////////////////////////////////////////////////////////////
ACTION_IF !FILE_EXISTS_IN_GAME ~m_moTHI1.lua~ THEN BEGIN
  COPY ~skills-and-abilities/lua/m_moTHI1.lua~  ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself
END

COPY ~skills-and-abilities/lua/m_moTHI7.lua~   ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself
COPY ~skills-and-abilities/spl/MO#BSK05.SPL~   ~override~ // Sets Detect Illusions to 100 EVERY level for Inquisitor

///////////////////////////////////////////////////////////////////////////
// Modify Base Inquisitor CLAB File to Assign Thief Skills Points        //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABPA03.2DA~ override
  REPLACE_TEXTUALLY ~GA_SPCL232~ ~****~ // Remove True Seeing
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = AP_MO#BSK05 END
  PRETTY_PRINT_2DA
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Set Max Skills Points in THIEFSCL.2da for the Inquisitor              //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~THIEFSCL.2DA~ ~override~
  SET_2DA_ENTRY 8 31 1 ~100~   // DETECT_ILLUSION
  PRETTY_PRINT_2DA
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Updates descriptions for Inquisitor                                   //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
OUTER_SPRINT old @6001 //old text: True Sight

OUTER_SPRINT new @6002 //new text: Detect Illusions

// Set the strref for the specific game first
ACTION_IF GAME_IS ~bgee~ BEGIN
  OUTER_SET inq_strref = 24288 // BGEE strref
END ELSE ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  OUTER_SET inq_strref = 25207 // BG2EE and EET strref
END ELSE ACTION_IF GAME_IS ~iwdee~ BEGIN
  OUTER_SET inq_strref = 37137 // IWDEE strref
END

ACTION_GET_STRREF ~%inq_strref%~ inq_description

OUTER_PATCH_SAVE inq_description ~%inq_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old%~ ~%new%~
END

STRING_SET_EVALUATE ~%inq_strref%~ ~%inq_description%~