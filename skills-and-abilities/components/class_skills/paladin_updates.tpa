///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Update Paladin Class and Kits                                         //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Import                                                                //
///////////////////////////////////////////////////////////////////////////
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Aura of Courage~) STR_VAR bam_file = SPWI210C RET MOPAL1A = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Aura of Despair~) STR_VAR bam_file = SPPR314C RET MOPAL2A = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Aura of Protection~) STR_VAR bam_file = SPPR111C RET MOPAL4A = icon END

ACTION_FOR_EACH aura IN
      1 // Aura of Courage
      2 // Aura of Despair
      4 // Aura of Protection
    BEGIN
      COPY ~skills-and-abilities/eff/class/MOPAL0%aura%.EFF~  ~override~ 
      COPY ~skills-and-abilities/spl/MOPAL0%aura%.SPL~        ~override~
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 = ~MOPAL%aura%A~ timing = 0 duration = 8 END
      COPY ~skills-and-abilities/spl/MOPALC%aura%.SPL~        ~override~
    END

COPY ~skills-and-abilities/eff/class/MO#PHL01.EFF~ ~override~ // Sanctify Strikes
COPY ~skills-and-abilities/spl/MOPHL01.SPL~        ~override~ // Sanctify Strikes
COPY ~skills-and-abilities/spl/MO#PHL01.SPL~       ~override~ // Sanctify Strikes
  SAY NAME1 @6003
  SAY UNIDENTIFIED_DESC @6004

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Smite~) STR_VAR bam_file = SPWI915C RET MOPAL3A = icon END
COPY ~skills-and-abilities/spl/MOPAL32.SPL~        ~override~ // Smite +3 Thac0/Dmg
COPY ~skills-and-abilities/spl/MOPAL31.SPL~        ~override~ // Smite +1 Thac0/Dmg
COPY ~skills-and-abilities/spl/MOPALC3.SPL~        ~override~ // Smite
  SAY NAME1 @6009
  SAY UNIDENTIFIED_DESC @6010
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 1 parameter2 = ~MOPAL3A~ timing = 0 duration = 6 probability1 = 100 END

///////////////////////////////////////////////////////////////////////////
// Set Base Paladin New Abilities                                        //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABPA01.2DA~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_MaxLevel = 3 STR_VAR f_Entry = AP_MOPALC1 END // Aura of Courage
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_Increment = 5 STR_VAR f_Entry = GA_MOPALC3 END // Smite
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = GA_MOPALC3 END // Smite
    PRETTY_PRINT_2DA
  BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Add HLAs                                                              //
///////////////////////////////////////////////////////////////////////////
ACTION_IF (GAME_IS ~bg2ee eet~) OR ((GAME_IS ~iwdee~) AND FILE_EXISTS_IN_GAME ~moiwd01.itm~) BEGIN
  LAF ADD_HLA INT_VAR class = 6 min_lev = 1 max_lev = 99 num_allowed = 1 STR_VAR ability = AP_MO#PHL01 END
  LAF ADD_HLA INT_VAR class = 6 min_lev = 1 max_lev = 99 num_allowed = 20 STR_VAR ability = GA_MOPALC3 END  
END

///////////////////////////////////////////////////////////////////////////
// Set Abilities for all Paladin Kits                                    //
///////////////////////////////////////////////////////////////////////////
 COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~pal_kitlist~ 10
  FOR (row = 3; row < pal_kitlist; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 8 kit_class
  
  PATCH_IF (%kit_class% = 6) BEGIN
    // Set New Paladin Abilities
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ BEGIN
      INNER_ACTION BEGIN
      // Add new abilities to all Paladin kits
      COPY_EXISTING ~%kit_clab%.2DA~ override
      PATCH_IF ("%kit_label%" STRING_EQUAL_CASE "Blackguard") BEGIN
        REPLACE_TEXTUALLY ~GA_SPCL103~ ~AP_MOPALC2~ // Aura of Despair
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_Increment = 5 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        PRETTY_PRINT_2DA
      END ELSE PATCH_IF ("%kit_label%" STRING_EQUAL_CASE "Cavalier") BEGIN
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_MaxLevel = 3 STR_VAR f_Entry = AP_MOPALC4 END // Aura of Protection
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_Increment = 5 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        PRETTY_PRINT_2DA
      END ELSE PATCH_IF ("%kit_label%" STRING_EQUAL_CASE "MO_REAVER") OR PATCH_IF ("%kit_label%" STRING_EQUAL_CASE "MO_OATHBREAKER") BEGIN
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_Increment = 5 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        PRETTY_PRINT_2DA
      END ELSE BEGIN
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_MaxLevel = 3 STR_VAR f_Entry = AP_MOPALC1 END // Aura of Courage
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_Increment = 5 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = GA_MOPALC3 END // Smite
        PRETTY_PRINT_2DA
      END
      BUT_ONLY
      END
    END
  END
  END
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Updates descriptions for Paladin                                      //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
OUTER_SPRINT old_pal    @6005
OUTER_SPRINT new_pal    @6006
OUTER_SPRINT old_black  @6007
OUTER_SPRINT new_black  @6008
OUTER_SPRINT old_cav    @6013
OUTER_SPRINT new_cav    @6014

// Set the strref for the specific game first
ACTION_IF GAME_IS ~bgee~ BEGIN
  OUTER_SET pal_strref = 9558 
  OUTER_SET black_strref = 28606
  OUTER_SET cav_strref = 24287
END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
  OUTER_SET pal_strref = 9558   
  OUTER_SET black_strref = 77514
  OUTER_SET cav_strref = 25206
END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
  OUTER_SET palbg1_strref = 209558 // BG1EE archived strref
  OUTER_SET pal_strref = 9558    
  OUTER_SET black_strref = 77514 
  OUTER_SET cav_strref = 25206   
END ELSE ACTION_IF GAME_IS ~iwdee~ BEGIN
  OUTER_SET pal_strref = 9558    
  OUTER_SET black_strref = 37145
  OUTER_SET cav_strref = 37133
END

// Paladin strref
ACTION_GET_STRREF ~%pal_strref%~ pal_description

OUTER_PATCH_SAVE pal_description ~%pal_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old_pal%~ ~%new_pal%~
END

STRING_SET_EVALUATE ~%pal_strref%~ ~%pal_description%~

// Blackguard strref
ACTION_GET_STRREF ~%black_strref%~ black_description

OUTER_PATCH_SAVE black_description ~%black_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old_black%~ ~%new_black%~
END

STRING_SET_EVALUATE ~%black_strref%~ ~%black_description%~

// Cavalier strref
ACTION_GET_STRREF ~%cav_strref%~ cav_description

OUTER_PATCH_SAVE cav_description ~%cav_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old_cav%~ ~%new_cav%~
END

STRING_SET_EVALUATE ~%cav_strref%~ ~%cav_description%~

// EET ONLY
ACTION_IF GAME_IS ~eet~ BEGIN
  ACTION_GET_STRREF ~%palbg1_strref%~ palbg1_description

  OUTER_PATCH_SAVE palbg1_description ~%palbg1_description%~ BEGIN
    REPLACE_TEXTUALLY ~%old_pal%~ ~%new_pal%~
  END

STRING_SET_EVALUATE ~%palbg1_strref%~ ~%palbg1_description%~
END

ACTION_FOR_EACH description IN 
    clastext 
    sodcltxt 
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%description%.2da~ BEGIN
      COPY_EXISTING ~%description%.2DA~ ~override~
      COUNT_2DA_COLS col_count
      COUNT_2DA_ROWS col_count rows
      FOR (index = 0; index < rows; ++index) BEGIN
        READ_2DA_ENTRY index 0 col_count row_name
        PATCH_IF "%row_name%" STRING_EQUAL_CASE "MO_REAVER" BEGIN
          READ_2DA_ENTRY index 4 col_count reaver_desc
          SPRINT old_reav1 @6015
          SPRINT new_reav1 @6016
          GET_STRREF %reaver_desc% reaver_text
          INNER_PATCH_SAVE reaver_text ~%reaver_text%~ BEGIN
            REPLACE_TEXTUALLY ~%old_reav1%~ ~%new_reav1%~
          END
          INNER_ACTION BEGIN
            STRING_SET_EVALUATE ~%reaver_desc%~ ~%reaver_text%~
          END
        END
      END
      BUT_ONLY
    END
  END