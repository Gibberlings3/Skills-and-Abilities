///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Overhaul Turn Undead                                                  //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Add Required Files to Override                                        //
///////////////////////////////////////////////////////////////////////////
// Lua files required for EEex
ACTION_IF FILE_EXISTS_IN_GAME ~M_B3THI7.lua~ THEN BEGIN
  COPY ~skills-and-abilities/lua/M_B3TUR1.lua~ ~override~ // Inquisitor Dispel Illusion
END ELSE BEGIN
  COPY ~skills-and-abilities/lua/M_B3TUR2.lua~ ~override~ // NO Inquisitor Dispel Illusion
END
COPY ~skills-and-abilities/lua/M_B3TUR3.lua~   ~override~ // Cleric, Fighter/Cleric, Cleric/Mage, FMC 
COPY ~skills-and-abilities/lua/M_B3TUR4.lua~   ~override~ // Cleric/Thief 
COPY ~skills-and-abilities/lua/M_B3TUR5.lua~   ~override~ // Cleric/Ranger

// Creates 2d6 + Character Level damage eff files for levels 1-9
FOR (i = 1; i < 10; ++i) BEGIN
  INNER_ACTION BEGIN
    COPY ~skills-and-abilities/eff/class/motur01.eff~ ~override/motur0%i%.eff~
      WRITE_LONG 0x1c i // Sets damage to character level
  END
END

// Creates 2d6 + Character Level damage eff files for levels 10-50
FOR (j = 10; j < 51; ++j) BEGIN
  INNER_ACTION BEGIN
    COPY ~skills-and-abilities/eff/class/motur01.eff~ ~override/motur%j%.eff~
      WRITE_LONG 0x1c j // Sets damage to character level
  END
END

ACTION_FOR_EACH charoveride IN
      13
      14
      15
      16
      18
      19
      20
      21
      22
      23
      24
      25
    BEGIN
      COPY ~skills-and-abilities/eff/class/mocha%charoveride%.eff~ ~override~
      COPY ~skills-and-abilities/spl/mocha%charoveride%.spl~ ~override~
    END

// New Turn Undead ability
COPY ~skills-and-abilities/spl/MOTURN1.SPL~    ~override~ // Turn Undead
  SAY NAME1 @9001
  SAY UNIDENTIFIED_DESC @9002

///////////////////////////////////////////////////////////////////////////
// Charisma Modifier                                                     //
///////////////////////////////////////////////////////////////////////////
APPEND ~splprot.2da~ ~STAT(CHA)=n%TAB%42%TAB%-1%TAB%1~

COPY_EXISTING ~splprot.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 0 ~stat~
      
      PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~STAT(CHA)=n~ BEGIN 
        SET cha_mod = %row%
      END
    END

COPY_EXISTING ~moturn1.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter2 = %cha_mod% END

///////////////////////////////////////////////////////////////////////////
// Give Paladin Turn Undead Ability                                      //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABPA01.2DA~ override
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 4 f_Increment = 4 STR_VAR f_Entry = GA_MOTURN1 END 
    PRETTY_PRINT_2DA
  BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Give Cleric Turn Undead Ability                                       //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABPR01.2DA~ override
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_Increment = 3 STR_VAR f_Entry = GA_MOTURN1 END 
    PRETTY_PRINT_2DA
  BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Give Cleric and Paladin Kits Turn Undead Ability                      //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~kitlist~ 10
  FOR (row = 3; row < kitlist; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~kitlist~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~kitlist~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~kitlist~ row 8 kit_class
  PATCH_IF (%kit_class% = 6) BEGIN
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ AND !("%kit_label%" STRING_EQUAL_CASE "INQUISITOR") BEGIN
      INNER_ACTION BEGIN
        COPY_EXISTING ~%kit_clab%.2DA~ override
          LPF set_clab_2da_entries INT_VAR f_MinLevel = 4 f_Increment = 4 STR_VAR f_Entry = GA_MOTURN1 END
          PRETTY_PRINT_2DA
        BUT_ONLY
      END
    END
  END ELSE PATCH_IF (%kit_class% = 3) BEGIN
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ BEGIN
      INNER_ACTION BEGIN
        COPY_EXISTING ~%kit_clab%.2DA~ override
          LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_Increment = 3 STR_VAR f_Entry = GA_MOTURN1 END
          PRETTY_PRINT_2DA
        BUT_ONLY
      END
    END
  END
  END

///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Updates descriptions for Paladin and Cleric                           //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
OUTER_SPRINT old_pal_turn    @9007
OUTER_SPRINT new_pal_turn    @9008
OUTER_SPRINT old_cle_turn    @9009
OUTER_SPRINT new_cle_turn    @9010

// Set the strref for the specific game first
ACTION_IF GAME_IS ~bgee~ BEGIN
  OUTER_SET pal_turn_strref = 9558 
  OUTER_SET cle_turn_strref = 9559
END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
  OUTER_SET pal_turn_strref = 9558   
  OUTER_SET cle_turn_strref = 9559
END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
  OUTER_SET palbg1_turn_strref = 209558 // BG1EE archived strref
  OUTER_SET pal_turn_strref = 9558
  OUTER_SET clebg1_turn_strref = 209559
  OUTER_SET cle_turn_strref = 9559    
END ELSE ACTION_IF GAME_IS ~iwdee~ BEGIN
  OUTER_SET pal_turn_strref = 9558
  OUTER_SET cle_turn_strref = 9559    
END

// Paladin strref
ACTION_GET_STRREF ~%pal_turn_strref%~ pal_turn_description

OUTER_PATCH_SAVE pal_turn_description ~%pal_turn_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old_pal_turn%~ ~%new_pal_turn%~
END

STRING_SET_EVALUATE ~%pal_turn_strref%~ ~%pal_turn_description%~

// Cleric strref
ACTION_GET_STRREF ~%cle_turn_strref%~ cle_turn_description

OUTER_PATCH_SAVE cle_turn_description ~%cle_turn_description%~ BEGIN
  REPLACE_TEXTUALLY ~%old_cle_turn%~ ~%new_cle_turn%~
END

STRING_SET_EVALUATE ~%cle_turn_strref%~ ~%cle_turn_description%~

// EET ONLY
ACTION_IF GAME_IS ~eet~ BEGIN
  ACTION_GET_STRREF ~%palbg1_turn_strref%~ palbg1_turn_description

  OUTER_PATCH_SAVE palbg1_turn_description ~%palbg1_turn_description%~ BEGIN
    REPLACE_TEXTUALLY ~%old_pal_turn%~ ~%new_pal_turn%~
  END

  STRING_SET_EVALUATE ~%palbg1_turn_strref%~ ~%palbg1_turn_description%~

  ACTION_GET_STRREF ~%clebg1_turn_strref%~ clebg1_turn_description

  OUTER_PATCH_SAVE clebg1_turn_description ~%clebg1_turn_description%~ BEGIN
    REPLACE_TEXTUALLY ~%old_cle_turn%~ ~%new_cle_turn%~
  END

  STRING_SET_EVALUATE ~%clebg1_turn_strref%~ ~%clebg1_turn_description%~
END
