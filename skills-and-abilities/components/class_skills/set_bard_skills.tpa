///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Sets Bard Thieving Skills                                             //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Add Required Files to Override                                        //
///////////////////////////////////////////////////////////////////////////
ACTION_IF !FILE_EXISTS_IN_GAME ~M_B3THI1.lua~ THEN BEGIN
  COPY ~skills-and-abilities/lua/M_B3THI1.lua~  ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself
END

// Lockpicking and Find/Disarm Traps ONLY
ACTION_IF bard_skill THEN BEGIN
  COPY ~skills-and-abilities/spl/MO#BSK01.SPL~    ~override~ // Assigns 5-10-10 thief skill points to bards
  COPY ~skills-and-abilities/spl/MO#BSK02.SPL~    ~override~ // Assigns 5-5-5 thief skill points to bards
  COPY ~skills-and-abilities/lua/M_B3THI2.lua~    ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself

// Lockpicking ONLY
END ELSE BEGIN
  COPY ~skills-and-abilities/spl/MO#BSK03.SPL~    ~override~ // Assigns 10 thief skills to Lockpicking
END

// Remove once EEex is updated for Bard Thief Skill Natural Progression
///////////////////////////////////////////////////////////////////////////
// Modify Base Bard CLAB File to Assign Thief Skills Points              //
///////////////////////////////////////////////////////////////////////////

// Lockpicking and Find/Disarm Traps ONLY
ACTION_IF bard_skill THEN BEGIN
  COPY_EXISTING ~CLABBA01.2DA~ override
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 5 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK01 END
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 6 f_MaxLevel = 20 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK02 END
    PRETTY_PRINT_2DA
  BUT_ONLY

// Lockpicking ONLY
END ELSE BEGIN
  COPY_EXISTING ~CLABBA01.2DA~ override
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK03 END
    PRETTY_PRINT_2DA
  BUT_ONLY
END

/* Once EEex is updated for Bard Thief Skill Natural Progression
///////////////////////////////////////////////////////////////////////////
// Modify THIEFSKL.2da to Allow Bards to Gain Thief Skill Progression    //
///////////////////////////////////////////////////////////////////////////
APPEND ~THIEFSKL.2da~ ~BARD 20 15~ // 20 Skill points at Level 1 then 15 skill points per level up
*/

///////////////////////////////////////////////////////////////////////////
// Set Max Skills Points in THIEFSCL.2da for Base Bard                   //
///////////////////////////////////////////////////////////////////////////

// Lockpicking and Find/Disarm Traps ONLY
ACTION_IF bard_skill THEN BEGIN
  COPY_EXISTING ~THIEFSCL.2DA~ ~override~
    SET_2DA_ENTRY 3 5 1 ~100~   // PICK_POCKETS
    SET_2DA_ENTRY 4 5 1 ~100~   // OPEN_LOCKS
    SET_2DA_ENTRY 5 5 1 ~100~   // FIND_TRAPS
    PRETTY_PRINT_2DA
  BUT_ONLY

// Lockpicking ONLY
END ELSE BEGIN
  COPY_EXISTING ~THIEFSCL.2DA~ ~override~
    SET_2DA_ENTRY 3 5 1 ~100~   // PICK_POCKETS
    SET_2DA_ENTRY 4 5 1 ~100~   // OPEN_LOCKS
    PRETTY_PRINT_2DA
  BUT_ONLY
END

///////////////////////////////////////////////////////////////////////////
// Remove 1st Level Skill Bonus for Base Bard                            //
///////////////////////////////////////////////////////////////////////////

// Lockpicking and Find/Disarm Traps ONLY
ACTION_IF bard_skill THEN BEGIN
  COPY_EXISTING ~CLASISKL.2DA~ ~override~
    SET_2DA_ENTRY 3 5 1 ~0~   // PICK_POCKETS
    PRETTY_PRINT_2DA
  BUT_ONLY
END

///////////////////////////////////////////////////////////////////////////
// Set Standard Bard Pick Pocket Skill Progression to 0                  //
///////////////////////////////////////////////////////////////////////////

// Lockpicking and Find/Disarm Traps ONLY
ACTION_IF bard_skill THEN BEGIN
  COPY_EXISTING ~SKILLBRD.2DA~ ~override~
    COUNT_2DA_ROWS 2 rows
    FOR (index = 1 ; index < rows ; ++index) BEGIN
      SET_2DA_ENTRY (index + 2) 1 1 ~0~
    END
    PRETTY_PRINT_2DA
  BUT_ONLY
END

///////////////////////////////////////////////////////////////////////////
// Set Skills for ALL Bard Kits                                          //
///////////////////////////////////////////////////////////////////////////
 COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~bard_kitlist~ 10
  FOR (row = 3; row < bard_kitlist; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 8 kit_class
  
  PATCH_IF (%kit_class% = 5) BEGIN
    // Modify Bard CLAB File to Assign Thief Skills Points Every Level
    // Remove once EEex is updated for Bard Thief Skill Natural Progression
    // delete lib.2da too when this gets removed
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ BEGIN
      INNER_ACTION BEGIN
      // Lockpicking and Find/Disarm Traps ONLY
      ACTION_IF bard_skill THEN BEGIN
        COPY_EXISTING ~%kit_clab%.2DA~ override
          LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 5 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK01 END
          LPF set_clab_2da_entries INT_VAR f_MinLevel = 6 f_MaxLevel = 20 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK02 END
          PRETTY_PRINT_2DA
        BUT_ONLY
      // Lockpicking ONLY
      END ELSE BEGIN
        COPY_EXISTING ~%kit_clab%.2DA~ override
          LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK03 END
          PRETTY_PRINT_2DA
        BUT_ONLY
      END
      END
    END

    /* Once EEex is updated for Bard Thief Skill Natural Progression
    // Modify THIEFSKL.2da to Allow Bard Kits to Gain Thief Skill Progression
    INNER_ACTION BEGIN
      APPEND ~THIEFSKL.2da~ ~%kit_label% 20 15~
    BUT_ONLY
    END
    */

    // Set Max Skills Points in THIEFSCL.2DA
    INNER_ACTION BEGIN
    COPY_EXISTING ~THIEFSCL.2DA~ ~override~
      READ_2DA_ENTRIES_NOW thiefscl 3
      COUNT_2DA_COLS cols
      FOR (col = 0; col < cols - 1; ++col) BEGIN
      READ_2DA_ENTRY_FORMER thiefscl 0 col column_name
      // Lockpicking and Find/Disarm Traps ONLY
      PATCH_IF bard_skill BEGIN
        PATCH_IF "%column_name%" STRING_EQUAL_CASE "%kit_label%" BEGIN
          SET_2DA_ENTRY 3 (col + 1) 1 ~100~   // PICK_POCKETS
          SET_2DA_ENTRY 4 (col + 1) 1 ~100~   // OPEN_LOCKS
          SET_2DA_ENTRY 5 (col + 1) 1 ~100~   // FIND_TRAPS
          PRETTY_PRINT_2DA
        END
      // Lockpicking ONLY
      END ELSE BEGIN
        PATCH_IF "%column_name%" STRING_EQUAL_CASE "%kit_label%" BEGIN
          SET_2DA_ENTRY 3 (col + 1) 1 ~100~   // PICK_POCKETS
          SET_2DA_ENTRY 4 (col + 1) 1 ~100~   // OPEN_LOCKS
          PRETTY_PRINT_2DA
        END
      END
      END
    END

    // Remove 1st Level Pick Pocket Bonus from ALL Bard Kits
    INNER_ACTION BEGIN
    COPY_EXISTING ~CLASISKL.2DA~ ~override~
      READ_2DA_ENTRIES_NOW clasiskl 3
      COUNT_2DA_COLS cols
      FOR (col = 0; col < cols - 1; ++col) BEGIN
      READ_2DA_ENTRY_FORMER clasiskl 0 col column_name
      // Lockpicking and Find/Disarm Traps ONLY
      PATCH_IF bard_skill BEGIN
        PATCH_IF "%column_name%" STRING_EQUAL_CASE "%kit_label%" BEGIN
          SET_2DA_ENTRY 3 (col + 1) 1 ~0~   // PICK_POCKETS
          PRETTY_PRINT_2DA
      END
      END
      END
    END
  END
  END

///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Updates Quests for Bards                                              //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Narlen Darkwalk - BGEE Thieves Guild                                  //
///////////////////////////////////////////////////////////////////////////
ACTION_IF GAME_IS ~bgee eet~ BEGIN
  COMPILE EVAL ~skills-and-abilities/dlg/narlenb.d~
END

///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Updates descriptions for Bards                                        //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
//
//open lock
 OUTER_SPRINT old @3501 //old text: Pick Pockets

//ACTION_IF (%bard_skill% = 0) BEGIN  // lockpicking is always in atm

 OUTER_SPRINT new @3502 //new text: Pick Pockets, Open Locks
 ACTION_IF GAME_IS ~iwdee~ THEN BEGIN

  ACTION_GET_STRREF 37236 bard_description

    OUTER_PATCH_SAVE bard_description ~%bard_description%~ BEGIN
      REPLACE_TEXTUALLY ~%old%~ ~%new%~

  END

  STRING_SET_EVALUATE 37236 ~%bard_description%~
 END //iwdee desc
 ACTION_IF GAME_IS ~bgee bg2ee eet~ THEN BEGIN

  ACTION_GET_STRREF 9562 bard_description

    OUTER_PATCH_SAVE bard_description ~%bard_description%~ BEGIN
      REPLACE_TEXTUALLY ~%old%~ ~%new%~

  END

  STRING_SET_EVALUATE 9562 ~%bard_description%~
 END //bgee/bg2ee/eet desc

//END

//find/remove traps
ACTION_IF (%bard_skill% =1) BEGIN  // lockpicking is always in atm
 OUTER_SPRINT new @3503 //new text: Pick Pockets, Find Traps
 ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
  ACTION_GET_STRREF 37236 bard_description

    OUTER_PATCH_SAVE bard_description ~%bard_description%~ BEGIN
      REPLACE_TEXTUALLY ~%old%~ ~%new%~

  END

  STRING_SET_EVALUATE 37236 ~%bard_description%~
 END //iwdee desc
 ACTION_IF GAME_IS ~bgee bg2ee eet~ THEN BEGIN

  ACTION_GET_STRREF 9562 bard_description

    OUTER_PATCH_SAVE bard_description ~%bard_description%~ BEGIN
      REPLACE_TEXTUALLY ~%old%~ ~%new%~

  END

  STRING_SET_EVALUATE 9562 ~%bard_description%~
 END //bgee/bg2ee/eet desc

END //bard_skill = 1