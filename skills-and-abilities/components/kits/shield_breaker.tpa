// Shield Breaker Kit 

ADD_KIT ~MO_SHIELDBREAKER~
  ~MO_SHIELDBREAKER 0 0 0 0 0 0 0 0~
  ~MO_SHIELDBREAKER 0 0 0 0 0 0 0 0 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 0 0 0 0 0 2 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~MO_SHIELDBREAKER 9 0 0 0 0 0~
  ~MO_SHIELDBREAKER 0 0 0 0 0 0~
  ~MO_SHIELDBREAKER 0 0 17 0 0 0~
  ~MO_SHIELDBREAKER 0 0 15 0 0 0~
  ~MO_SHIELDBREAKER 1 1 1 1 1 1 1 1 1~
  ~MO_SHIELDBREAKER 0 1 1 1 1 0~
  ~skills-and-abilities/2da/mo_shb01.2DA~
  ~K_F_H K_F_D K_F_G K_F_E K_F_HE K_F_HL K_F_HO~
  ~0x00004000 2~
  ~Fi0~
  ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  SAY @5524
  SAY @5525
  SAY @5526

LAF fl#add_kit_ee STR_VAR kit_name = MO_SHIELDBREAKER END

OUTER_SET shieldbreaker_code = (0x4000 + %MO_SHIELDBREAKER%)

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  SPRINT weapon_name ~%SOURCE_FILE%~
  READ_SHORT 0x31 prof ELSE 0
  READ_SHORT 0x1c type
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  //SET ranged = 0

  // Restrict Ranged Weapons and Shields
  PATCH_IF (prof = 104) OR  // Longbow
        (prof = 105) OR     // Shortbow
        (prof = 103) OR     // Crossbow
        (prof = 106) OR     // Dart
        (prof = 107) OR     // Sling
        (type = 12) BEGIN   // Shields
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %shieldbreaker_code% parameter2 = 9 power = 0 timing = 2 END
  END ELSE BEGIN
    // Thanks to Cam for providing the below FOR loop to check for ranged weapons header.
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      READ_SHORT (abil_off +        (index * 0x38)) type
      PATCH_IF (type = 2) BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %shieldbreaker_code% parameter2 = 9 power = 0 timing = 2 END
        SET index = abil_num // kills loop, prevents patching if ranged item
      END
    END
  END
  BUT_ONLY

COPY ~skills-and-abilities/2da/mo_shb01.2DA~     ~override~ // kit
COPY ~skills-and-abilities/spl/kits/moshb01.spl~ ~override~ // Sunder
  SAY NAME1 @5527
  SAY UNIDENTIFIED_DESC @5528
COPY ~skills-and-abilities/spl/kits/moshb02.spl~ ~override~ // -3 Damage
COPY ~skills-and-abilities/spl/kits/moshb03.spl~ ~override~ // -10% DR per Hit Spell
COPY ~skills-and-abilities/eff/kits/moshb3a.eff~ ~override~ // -10% Slashing Effect
COPY ~skills-and-abilities/eff/kits/moshb3b.eff~ ~override~ // -10% Crushing Effect
COPY ~skills-and-abilities/eff/kits/moshb3c.eff~ ~override~ // -10% Piercing Effect
COPY ~skills-and-abilities/eff/kits/moshb3d.eff~ ~override~ // -10% Missile Effect