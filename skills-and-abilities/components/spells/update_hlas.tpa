///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
//  Update Existing HLAs                                                 //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
OUTER_SPRINT oldgww @8031
OUTER_SPRINT newgww @8032

COPY ~skills-and-abilities/spl/SPCL904.SPL~ ~override~ // Resist Magic Update
  SAY NAME1 @8037
  SAY NAME2 @8037
  SAY UNIDENTIFIED_DESC @8035

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~War Cry~) STR_VAR bam_file = SPCL941C RET MOWCR1A = icon END
COPY ~skills-and-abilities/spl/SPCL908.SPL~ ~override~ // War Cry Update
  SAY NAME1 @8038
  SAY NAME2 @8038
  SAY UNIDENTIFIED_DESC @8036
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = ~MOWCR1A~ END

  // Need to create a new spellstate for this in my ai scripts
  
// Alter Greater Whirlwind to last 3 rounds
LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WARRIOR_GREATER_WHIRLWIND~ RET spell_res END
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell_res%.SPL~ BEGIN
    COPY_EXISTING ~%spell_res%.SPL~ ~override~ 
      LPF ALTER_EFFECT INT_VAR match_duration = 6 duration = 18 END

      READ_LONG 0x50 desc_strref
        PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
          READ_STRREF 0x50 desc
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~%oldgww%~ ~%newgww%~
          END
          SAY_EVALUATED 0x50 ~%desc%~
        END
  END

// Update HLA Abilities so they do NOT get dispelled
ACTION_FOR_EACH hardi IN
      DW#WISH4 // Hardiness
      SPCL907  // Hardiness
      SPWISH12 // Hardiness
      SPCL904  // Resist Magic
      SPCL917  // Avoid Death
      SPCL913  // Evasion
      SPCL914  // Greater Evasion
      SPCL916  // Assassination
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%hardi%.SPL~ BEGIN
        COPY_EXISTING ~%hardi%.SPL~ ~override~
          WRITE_BYTE 0x25 0
          WRITE_BYTE 0x27 0
      END 
END